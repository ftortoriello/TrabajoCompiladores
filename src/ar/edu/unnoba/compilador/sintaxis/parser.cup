package ar.edu.unnoba.compilador.sintaxis;

import java_cup.runtime.*;
import java.util.ArrayList;
import java.util.List;
import java.io.FileReader;
import java.util.logging.Level;
import java.util.logging.Logger;

import ar.edu.unnoba.compilador.token.Token;

import ar.edu.unnoba.compilador.ast.base.Programa;
// import ar.edu.unnoba.compilador.ast.base.Bloque;
import ar.edu.unnoba.compilador.ast.base.Constante;
import ar.edu.unnoba.compilador.ast.base.Tipo;

class Parser;

parser code
{:
    public void syntax_error(Symbol s) {
        System.out.println("Error en la línea "+ (s.left+1)+ " Columna "+ s.right+ ". Símbolo '"
        +s.value+"'. Símbolo n° "+s.sym+ " no reconocido." );
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception {
        System.out.println("Error en la línea "+ (s.left+1)+ " Columna "+ s.right+ ". Símbolo '"
        +s.value+"'. Símbolo n° "+s.sym+ " no reconocido." );
    }
:};

terminal String PR_MAIN_IS, PR_END_PUNTO,
    PR_VARIABLE, PR_IS, PR_FUNCTION, PR_RETURN,
    PR_BEGIN, PR_END,
    PR_IF, PR_THEN, PR_ELSE, PR_WHEN, PR_WHILE,
    PR_FOR, PR_FROM, PR_TO, PR_BY, PR_DO, PR_BREAK, PR_CONTINUE,
    PR_WRITELN, PR_WRITE, PR_READ_INTEGER, PR_READ_FLOAT, PR_READ_BOOLEAN,
    OP_ARIT_SUMA, OP_ARIT_RESTA, OP_ARIT_PROD_O_DIV, OP_COMPARACION,
    OP_LOG_BIN_OR, OP_LOG_BIN_AND, OP_LOG_UNA_NOT,
    PAR_ABRE, PAR_CIERRA, PR_COMA, PUNTO_Y_COMA, IGUAL,
    IDENTIFICADOR, TIPO_DE_DATO,
    CTE_BOOLEANA, FLOTANTE, ENTERO, CADENA;

// TODO: cambiar los no terminales de String a sus tipos correspondientes
nonterminal Programa programa;

// nonterminal Constante constante;

nonterminal String lst_decs_vars_y_funcs, dec_var, dec_fun,
    arg_dec, arg_dec_default, lst_args_dec, lst_args_dec_default,
    bloque, lst_sentencias, sentencia, bloque_when_is,
    expresion, termino, factor, constante,
    invocacion_funcion, lst_args_invocacion;

precedence left OP_LOG_BIN_OR, OP_ARIT_SUMA, OP_ARIT_RESTA;
precedence left OP_LOG_BIN_AND, OP_ARIT_PROD_O_DIV;
precedence left OP_LOG_UNA_NOT;

// los "else" se asocian implícitamente al último "if"
precedence right PR_ELSE;

precedence left PR_IS;

start with programa;

programa ::=
        lst_decs_vars_y_funcs:decs PR_MAIN_IS:pr_mainis lst_sentencias:sentencias PR_END_PUNTO:pr_end {:
            // RESULT = String.join(" ", decs, pr_mainis, sentencias, pr_end);
            // System.out.println("REGLA 0.1 -> lst_decs_vars_y_funcs PR_MAIN_IS lst_sentencias PR_END_PUNTO (" + RESULT + ") -> programa");
            RESULT = new Programa("Test");
        :}
//        |
//        PR_MAIN_IS:pr_mainis lst_sentencias:sentencias PR_END_PUNTO:pr_end {:
//            // RESULT = String.join(" ", pr_mainis, sentencias, pr_end);
//            // System.out.println("REGLA 0.2 -> PR_MAIN_IS lst_sentencias PR_END_PUNTO (" + RESULT + ") -> programa");
//            RESULT = new Programa(new Bloque());
//        :} |
//        lst_decs_vars_y_funcs:decs PR_MAIN_IS:pr_mainis PR_END_PUNTO:pr_end {:
//            // programa con declaraciones pero sin cuerpo principal
//            // RESULT = String.join(" ", decs, pr_mainis, pr_end);
//            // System.out.println("REGLA 0.3 -> lst_decs_vars_y_funcs PR_MAIN_IS PR_END_PUNTO (" + RESULT + ") -> programa");
//            RESULT = new Programa(new Bloque());
//        :} |
//        PR_MAIN_IS:pr_mainis PR_END_PUNTO:pr_end {:
//            // programa mínimo, sin cuerpo
//            // RESULT = String.join(" ", pr_mainis, pr_end);
//            // System.out.println("REGLA 0.4 -> PR_MAIN_IS PR_END_PUNTO (" + RESULT + ") -> programa");
//            RESULT = new Programa(new Bloque());
//        :}
        ;

lst_decs_vars_y_funcs ::=
        dec_var:dec_var PUNTO_Y_COMA:pyc lst_decs_vars_y_funcs:decs {:
             RESULT = String.join(" ", dec_var, pyc, decs);
             System.out.println("REGLA 1.1: dec_var (" + RESULT + ") -> lst_decs_vars_y_funcs");
        :} |
        dec_fun:dec_fun lst_decs_vars_y_funcs:decs {:
            RESULT = String.join(" ", dec_fun, decs);
            System.out.println("REGLA 1.2: dec_fun (" + RESULT + ") -> lst_decs_vars_y_funcs");
        :} |
        dec_var:dec_var PUNTO_Y_COMA:pyc {:
            RESULT = String.join(" ", dec_var, pyc);
            System.out.println("REGLA 1.3: dec_var (" + RESULT + ") -> lst_decs_vars_y_funcs");
        :} |
        dec_fun:dec_fun {:
            RESULT = dec_fun;
            System.out.println("REGLA 1.4: dec_fun (" + RESULT + ") -> lst_decs_vars_y_funcs");
        :};

dec_var ::=
        PR_VARIABLE:pr_var arg_dec:arg_dec {:
             RESULT = String.join(" ", pr_var, arg_dec);
             System.out.println("REGLA 1.3.1: PR_VARIABLE arg_dec PUNTO_Y_COMA (" + RESULT + ") -> dec_var");
        :} |
        PR_VARIABLE:pr_var arg_dec_default:arg_dec_default {:
             RESULT = String.join(" ", pr_var, arg_dec_default);
             System.out.println("REGLA 1.3.2: PR_VARIABLE arg_dec_default PUNTO_Y_COMA (" + RESULT + ") -> dec_var");
        :};

arg_dec ::=
        IDENTIFICADOR:id PR_IS:pr_is TIPO_DE_DATO:td  {:
            RESULT = String.join(" ", id, pr_is, td);
            System.out.println("REGLA 1.3.1.1: IDENTIFICADOR PR_IS TIPO_DE_DATO (" + RESULT + ") -> arg_dec");
        :};

arg_dec_default ::=
        IDENTIFICADOR:id PR_IS:pr_is TIPO_DE_DATO:td IGUAL:igual expresion:exp {:
            RESULT = String.join(" ", id, pr_is, td, igual, exp);
            System.out.println("REGLA 1.3.2.1: IDENTIFICADOR PR_IS TIPO_DE_DATO " +
                               "IGUAL expresion (" + RESULT + ") -> arg_dec_default");
        :};

dec_fun ::=
        PR_FUNCTION:pr_funcion IDENTIFICADOR:id PAR_ABRE:pa PAR_CIERRA:pc PR_RETURN:ret TIPO_DE_DATO:td bloque:bloque PUNTO_Y_COMA:pyc {:
            RESULT = String.join(" ", pr_funcion, id, pa, pc, ret, td, bloque, pyc);
            System.out.println("REGLA 1.4.1: PR_FUNCTION IDENTIFICADOR PAR_ABRE PAR_CIERRA PR_RETURN " +
                               "TIPO_DE_DATO bloque PUNTO_Y_COMA:pyc (" + RESULT + ") -> dec_fun");
        :} |
        PR_FUNCTION:pr_funcion IDENTIFICADOR:id PAR_ABRE:pa lst_args_dec:args PAR_CIERRA:pc PR_RETURN:ret TIPO_DE_DATO:td bloque:bloque PUNTO_Y_COMA:pyc {:
            RESULT = String.join(" ", pr_funcion, id, pa, args, pc, ret, td, bloque, pyc);
            System.out.println("REGLA 1.4.2: PR_FUNCTION IDENTIFICADOR PAR_ABRE lst_args_dec PAR_CIERRA " +
                               "PR_RETURN TIPO_DE_DATO bloque PUNTO_Y_COMA:pyc (" + RESULT + ") -> dec_fun");
        :};

lst_args_dec ::=
        arg_dec:arg PR_COMA:pr_coma lst_args_dec:args {:
            RESULT = String.join(" ", arg, pr_coma, args);
            System.out.println("REGLA 1.4.2.1: arg_dec PR_COMA lst_args_dec (" + RESULT + ") -> lst_args_dec");
        :} |
        arg_dec:arg {:
            RESULT = arg;
            System.out.println("REGLA 1.4.2.2: arg_dec (" + RESULT + ") -> lst_args_dec");
        :} |
        arg_dec_default:arg PR_COMA:pr_coma lst_args_dec_default:args {:
            RESULT = String.join(" ", arg, pr_coma, args);
            System.out.println("REGLA 1.4.2.3: arg_dec_default PR_COMA lst_args_dec_default (" + RESULT + ") -> lst_args_dec");
        :} |
        arg_dec_default:arg {:
            RESULT = String.join(" ", arg);
            System.out.println("REGLA 1.4.2.4: arg_dec_default (" + RESULT + ") -> lst_args_dec");
        :};

lst_args_dec_default ::=
        arg_dec_default:arg PR_COMA:pr_coma lst_args_dec_default:args {:
            RESULT = String.join(" ", arg, pr_coma, args);
            System.out.println("REGLA 1.4.2.3.1: arg_dec_default PR_COMA lst_args_dec_default (" + RESULT + ") -> lst_args_dec_default");
        :} |
        arg_dec_default:arg {:
            RESULT = arg;
            System.out.println("REGLA 1.4.2.3.2: arg_dec_default (" + RESULT + ") -> lst_args_dec_default");
        :};


// sentencia simple o bloque begin/end para implementación de funciones y estructuras de selección e iteración
bloque ::=
        sentencia:sent {:
            RESULT = String.join(" ", sent);
            System.out.println("REGLA 2.1: sentencia (" + RESULT + ") -> bloque");
        :} |
        PR_BEGIN:begin lst_sentencias:sentencias PR_END:pr_end {:
            RESULT = String.join(" ", begin, sentencias, pr_end);
            System.out.println("REGLA 2.2: PR_BEGIN lst_sentencias PR_END (" + RESULT + ") -> bloque");
        :};

lst_sentencias ::=
        sentencia:sent PUNTO_Y_COMA:pyc lst_sentencias:sentencias {:
            RESULT = String.join(" ", sent, pyc, sentencias);
            System.out.println("REGLA 3.1: sentencia PUNTO_Y_COMA lst_sentencias (" + RESULT + ") -> lst_sentencias");
        :} |
        sentencia:sent PUNTO_Y_COMA:pyc {:
            RESULT = String.join(" ", sent, pyc);
            System.out.println("REGLA 3.2: sentencia PUNTO_Y_COMA (" + RESULT + ") -> lst_sentencias");
        :};

sentencia ::=
        // asignación
        IDENTIFICADOR:id IGUAL:igual expresion:expr {:
            RESULT = String.join(" ", id, igual, expr);
            System.out.println("REGLA 4.1: IDENTIFICADOR IGUAL expresion (" + RESULT + ") -> sentencia");
        :} |
        invocacion_funcion:invo_fun {:
            RESULT = invo_fun;
            System.out.println("REGLA 4.2: invocacion_funcion(" + RESULT + ") -> sentencia");
        :} |
        // las variables se pueden definir en cualquier punto del programa
        dec_var:dec_var {:
            RESULT = dec_var;
            System.out.println("REGLA 4.3: dec_var (" + RESULT + ") -> sentencia");
        :} |
        PR_IF:pr_if expresion:exp PR_THEN:pr_then bloque:bloque {:
            RESULT = String.join(" ", pr_if, exp, pr_then, bloque);
            System.out.println("REGLA 4.4: PR_IF expresion PR_THEN bloque ("
            + RESULT + ") -> sentencia");
        :} |
        PR_IF:pr_if expresion:exp PR_THEN:pr_then bloque:bloque1 PR_ELSE:pr_else bloque:bloque2 {:
            RESULT = String.join(" ", pr_if, exp, pr_then, bloque1, pr_else, bloque2);
            System.out.println("REGLA 4.5: PR_IF expresion PR_THEN bloque PR_ELSE bloque ("
            + RESULT + ") -> sentencia");
        :} |
        PR_WHEN:when expresion:exp bloque_when_is:bloque_is {:
            RESULT = String.join(" ", when, exp, when, bloque_is);
            System.out.println("REGLA 4.6: PR_WHEN expresion bloque_when_is ("
            + RESULT + ") -> sentencia");
        :} |
        PR_WHEN:when expresion:exp bloque_when_is:bloque_is PR_ELSE:pr_else bloque:bloque {:
            RESULT = String.join(" ", when, exp, when, bloque_is, pr_else, bloque);
            System.out.println("REGLA 4.7: PR_WHEN expresion bloque_when_is PR_ELSE bloque ("
            + RESULT + ") -> sentencia");
        :} |
        PR_WHILE:pr_while expresion:exp PR_DO:pr_do bloque:bloque {:
            RESULT = String.join(" ", pr_while, exp, pr_do, bloque);
            System.out.println("REGLA 4.8: PR_WHILE expresion PR_DO sentencia (" + RESULT + ")"
            + " -> sentencia");
        :} |
        PR_FOR:pr_for IDENTIFICADOR:id PR_FROM:pr_from ENTERO:num_ini PR_TO:pr_to ENTERO:num_fin PR_DO:pr_do bloque:bloque {:
            RESULT = String.join(" ", pr_for, id, pr_from, num_ini, pr_to, num_fin, pr_do, bloque);
            System.out.println("REGLA 4.9: PR_FOR IDENTIFICADOR PR_FROM ENTERO PR_TO ENTERO PR_DO bloque ("
                + RESULT + ")" + " -> sentencia");
        :} |
        PR_FOR:pr_for IDENTIFICADOR:id PR_FROM:pr_from ENTERO:num_ini PR_TO:pr_to ENTERO:num_fin PR_BY:pr_by ENTERO:salto PR_DO:pr_do bloque:bloque {:
            RESULT = String.join(" ", pr_for, id, pr_from, num_ini, pr_to, num_fin, pr_by, salto, pr_do, bloque);
            System.out.println("REGLA 4.10: PR_FOR IDENTIFICADOR PR_FROM ENTERO PR_TO ENTERO PR_DO bloque ("
                + RESULT + ")" + " -> sentencia");
        :} |
        PR_RETURN:ret expresion:expr {:
            RESULT = String.join(" ", ret, expr);
            System.out.println("REGLA 4.11: PR_RETURN expresion (" + RESULT + ") -> sentencia");
        :} |
        PR_BREAK:pr_break {:
            RESULT = pr_break;
            System.out.println("REGLA 4.12: PR_BREAK (" + RESULT + ") -> sentencia");
        :} |
        PR_CONTINUE:pr_cont {:
            RESULT = pr_cont;
            System.out.println("REGLA 4.13: PR_CONTINUE (" + RESULT + ") -> sentencia");
        :};

bloque_when_is ::=
        PR_IS:pr_is OP_COMPARACION:comp expresion:exp PR_THEN:pr_then bloque:bloque {:
            RESULT = String.join(" ", pr_is, comp, exp, pr_then, bloque);
            System.out.println("REGLA 4.6.1: PR_IS OP_COMPARACION expresion:exp PR_THEN bloque ("
            + RESULT + ") -> bloque_when_is");
        :} |
        PR_IS:pr_is OP_COMPARACION:comp expresion:exp PR_THEN:pr_then bloque:bloque bloque_when_is:bloque_when {:
            RESULT = String.join(" ", pr_is, comp, exp, pr_then, bloque, bloque_when);
            System.out.println("REGLA 4.6.2: PR_IS OP_COMPARACION expresion PR_THEN bloque bloque_when_is ("
            + RESULT + ") -> bloque_when_is");
        :};

expresion ::=
        expresion:expr OP_ARIT_SUMA:op_suma termino:termino {:
            RESULT = String.join(" ", expr, op_suma, termino);
            System.out.println("REGLA 5.1: expresion OP_ARIT_SUMA termino (" + RESULT + ") -> expresion");
        :} |
        expresion:expr OP_ARIT_RESTA:op_resta termino:termino   {:
            RESULT = String.join(" ", expr, op_resta, termino);
            System.out.println("REGLA 5.2: expresion OP_ARIT_RESTA termino (" + RESULT + ") -> expresion");
        :} |
        expresion:expr OP_LOG_BIN_OR:op_or termino:expr_and {:
            RESULT = String.join(" ", expr, op_or, expr_and);
            System.out.println("REGLA 5.3: expresion_logica_or OP_LOG_BIN_OR termino (" + RESULT +") -> expresion");
        :} |
        termino:expr {:
            RESULT = expr;
            System.out.println("REGLA 5.4: termino (" + RESULT + ") -> expresion");
        :};

termino ::=
        termino:termino OP_ARIT_PROD_O_DIV:op_prod_div factor:factor {:
            RESULT = String.join(" ", termino, op_prod_div, factor);
            System.out.println("REGLA 6.1: termino OP_ARIT_PROD_O_DIV factor (" + RESULT + ") -> termino");
        :} |
        termino:termino OP_LOG_BIN_AND:op_and factor:factor {:
            RESULT = String.join(" ", termino, op_and, factor);
            System.out.println("REGLA 6.2: termino OP_LOG_BIN_AND factor (" + RESULT + ") -> termino");
        :} |
        factor:factor {:
            RESULT = factor;
            System.out.println("REGLA 6.3: factor (" + RESULT + ") -> termino");
        :};

factor ::=
        constante:cte {:
            RESULT = cte;
            System.out.println("REGLA 7.1: constante (" + RESULT + ") -> factor");
        :} |
        // resta unaria
        OP_ARIT_RESTA:menos constante:cte {:
            RESULT = String.join(" ", menos, cte);
            System.out.println("REGLA 7.2: OP_ARIT_RESTA constante (" + RESULT + ") -> factor");
        :} |
        factor:factor OP_COMPARACION:op_comp constante:cte {:
            RESULT = String.join(" ", factor, op_comp, cte);
            System.out.println("REGLA 7.3: factor OP_COMPARACION constante (" + RESULT + ") -> factor");
        :} |
        OP_LOG_UNA_NOT:op_not factor:factor {:
            RESULT = String.join(" ", op_not, factor);
            System.out.println("REGLA 7.4: OP_LOG_UNA_NOT constante (" + RESULT + ") -> factor");
        :} |
        PAR_ABRE:pa expresion:exp PAR_CIERRA:pc {:
            RESULT = String.join(" ", pa, exp, pc);
            System.out.println("REGLA 7.5: PA_ABRE expresion PA_CIERRA (" + RESULT + ") -> factor");
        :};

constante ::=
        ENTERO:num_int {:
            RESULT = new Constante(num_int, Tipo.INTEGER);
        :} |
        FLOTANTE:num_float {:
            RESULT = new Constante(num_float, Tipo.FLOAT);
        :} |
        CTE_BOOLEANA:cte_booleana {:
            RESULT = new Constante(cte_booleana, Tipo.BOOL);
        :} |
        IDENTIFICADOR:id {:
            // TODO: No sé si es correcto que el identificador y la llamada estén acá, no son "constantes"?
            // Más allá de eso, los tipos del identificador y la invocación a función serían desconocidos
            // en la primera pasada, y se obtienen en una posterior mediante el transformer?
            RESULT = new Constante(id, Tipo.UNKNOWN);
        :} |
        invocacion_funcion:fun {:
            RESULT = fun;
        :};

invocacion_funcion ::=
        PR_WRITELN:writeln PAR_ABRE:pa expresion:op PAR_CIERRA:pc {:
            RESULT = String.join(" ", writeln, pa, op, pc);
            System.out.println("REGLA 10.1: PR_WRITELN PAR_ABRE expresion PAR_CIERRA (" + RESULT + ") -> invocacion_funcion");
        :} |
        PR_WRITELN:writeln PAR_ABRE:pa CADENA:c PAR_CIERRA:pc {:
            RESULT = String.join(" ", writeln, pa, c, pc);
            System.out.println("REGLA 10.2: PR_WRITELN PAR_ABRE CADENA PAR_CIERRA (" + RESULT + ") -> invocacion_funcion");
        :} |
        PR_WRITE:write PAR_ABRE:pa expresion:op PAR_CIERRA:pc {:
            RESULT = String.join(" ", write, pa, op, pc);
            System.out.println("REGLA 10.3: PR_WRITE PAR_ABRE expresion PAR_CIERRA (" + RESULT + ") -> invocacion_funcion");
        :} |
        PR_WRITE:write PAR_ABRE:pa CADENA:c PAR_CIERRA:pc {:
            RESULT = String.join(" ", write, pa, c, pc);
            System.out.println("REGLA 10.4: PR_WRITE PAR_ABRE CADENA PAR_CIERRA (" + RESULT + ") -> invocacion_funcion");
        :} |
        PR_READ_INTEGER:read_integer PAR_ABRE:pa PAR_CIERRA:pc {:
            RESULT = String.join(read_integer, pa, pc);
            System.out.println("REGLA 10.5: PR_READ_INTEGER PAR_ABRE PAR_CIERRA (" + RESULT + ") -> invocacion_funcion");
        :} |
        PR_READ_FLOAT:read_float PAR_ABRE:pa PAR_CIERRA:pc {:
            RESULT = String.join(read_float, pa, pc);
            System.out.println("REGLA 10.6: PR_READ_FLOAT PAR_ABRE PAR_CIERRA (" + RESULT + ") -> invocacion_funcion");
        :} |
        PR_READ_BOOLEAN:read_boolean PAR_ABRE:pa PAR_CIERRA:pc {:
            RESULT = String.join(read_boolean, pa, pc);
            System.out.println("REGLA 10.7: PR_READ_BOOLEAN PAR_ABRE PAR_CIERRA (" + RESULT + ") -> invocacion_funcion");
        :} |
        IDENTIFICADOR:id PAR_ABRE:pa PAR_CIERRA:pc {:
            RESULT = String.join(" ", id, pa, pc);
            System.out.println("REGLA 10.8: IDENTIFICADOR PAR_ABRE PAR_CIERRA (" + RESULT + ") -> invocacion_funcion");
        :} |
        IDENTIFICADOR:id PAR_ABRE:pa lst_args_invocacion:args PAR_CIERRA:pc {:
            RESULT = String.join(" ", id, pa, args, pc);
            System.out.println("REGLA 10.9: IDENTIFICADOR PAR_ABRE lst_args_invocacion PAR_CIERRA (" + RESULT + ") -> invocacion_funcion");
        :};

lst_args_invocacion ::=
        expresion:op PR_COMA:pr_coma lst_args_invocacion:args {:
            RESULT = String.join(" ", op, pr_coma, args);
            System.out.println("REGLA 10.9.1: expresion lst (" + RESULT + ") -> lst_args_invocacion");
        :} |
        expresion:op {:
            RESULT = op;
            System.out.println("REGLA 10.9.2: expresion (" + RESULT + ") -> lst_args_invocacion");
        :};
